!                   ******************************
                    SUBROUTINE ORG_CHARAC_TYPE_OIL
!                   ******************************
!
     &(OIL_CHARAC)
!
!***********************************************************************
! PARALLEL   V6P3                                   21/08/2010
!***********************************************************************
!
!brief    MPI TYPE FOR TYPE CHARAC_TYPE - CHARACTERISTICS /
!
!history  C. DENIS
!+        01/07/2011
!+        V6P1
!+
!
!history  N.DURAND (HRW), S.E.BOURBAN (HRW)
!+        13/07/2010
!+        V6P0
!+   Translation of French comments within the FORTRAN sources into
!+   English comments
!
!history  N.DURAND (HRW), S.E.BOURBAN (HRW)
!+        21/08/2010
!+        V6P0
!+   Creation of DOXYGEN tags for automated documentation and
!+   cross-referencing of the FORTRAN sources
!
!history  J-M HERVOUET
!+        22/06/2012
!+        V6P2
!+   DX,DY and DZ added. Problem of integer 8 treated as a double
!+   solved. Much clearer now.
!+
!history  J-M HERVOUET
!+        04/10/2012
!+        V6P3
!+   NOMB = 0 now allowed
!
!~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!| NOMB           |<---| NUMBER OF VARIABLES
!| TRACE          |<---| IF .TRUE. TRACE EXECUTION
!| CHARACTERISTIC |--->| DATATYPE FOR CHARACTERISTIC
!~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!
      USE DECLARATIONS_PARALLEL
      USE DECLARATIONS_SPECIAL
      USE INTERFACE_PARALLEL, ONLY : P_MPI_TYPE_GET_EXTENT,
     &     P_MPI_TYPE_CREATE_STRUCT, P_MPI_TYPE_COMMIT
      IMPLICIT NONE
!
!+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
!
      INTEGER, INTENT(INOUT) :: OIL_CHARAC
!
!+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+-+
!
      INTEGER, PARAMETER :: MAX_BASKET_SIZE=10
!
!     OIL_TYPE IS NOT USED HERE !!!  NOT USED SAVE FOR TRACE
!
!     TYPE OIL_TYPE
!       SEQUENCE
!       INTEGER :: MYPID ! PARTITION OF THE TRACEBACK ORIGIN (HEAD)
!       INTEGER :: NEPID ! THE NEIGHBOUR PARTITION THE TRACEBACK ENTERS TO
!       INTEGER :: INE   ! THE LOCAL 2D ELEMENT NR THE TRACEBACK ENTERS IN THE NEIGBOUR PARTITION
!       INTEGER :: KNE   ! THE LOCAL LEVEL THE TRACEBACK ENTERS IN THE NEIGBOUR PARTITION
!       INTEGER :: IOR   ! THE POSITION OF THE TRAJECTORY -HEAD- IN MYPID [THE 2D/3D NODE OF ORIGIN]
!       INTEGER :: STATE   ! CURRENT RUNGE-KUTTA STEPS PASSED AS COLLECTED
!       INTEGER :: TPSECH   ! TOTAL RUNGE-KUTTA STEPS
!       INTEGER :: IFR   ! FREQUENCY
!       DOUBLE PRECISION :: SURFACE  ! THE (X,Y,Z,F)-POSITION
!       DOUBLE PRECISION :: MASS0
!       DOUBLE PRECISION :: MASS
!       DOUBLE PRECISION :: MASS_EVAP
!       DOUBLE PRECISION :: MASS_DISS
!       DOUBLE PRECISION :: MASS_HAP(MAX_BASKET_SIZE)
!       DOUBLE PRECISION :: MASS_COMPO(MAX_BASKET_SIZE)
!       DOUBLE PRECISION :: TB_HAP(MAX_BASKET_SIZE)
!       DOUBLE PRECISION :: TB_COMPO(MAX_BASKET_SIZE)
!       DOUBLE PRECISION :: SOL_HAP(MAX_BASKET_SIZE)
!       DOUBLE PRECISION :: SOL_COMPO(MAX_BASKET_SIZE)
!     END TYPE OIL_CHARAC_TYPE
!
!     ARRAY OF DISPLACEMENTS BETWEEN BASIC COMPONENTS, HERE INITIALISED ONLY
!
!      INTEGER (KIND=MPI_ADDRESS_KIND), DIMENSION(20) :: CH_DELTA
      INTEGER (KIND=MY_ADDRESS_KIND), DIMENSION(20) :: CH_DELTA
!
!     ARRAY OF BLOCKLENGTHS OF TYPE COMPONENTS, BASKET INITIALISED TO 1
!
      INTEGER, DIMENSION(20) :: CH_BLENGTH
!     ARRAY OF COMPONENT TYPES IN TERMS OF THE MPI COMMUNICATION
      INTEGER, DIMENSION(20) :: CH_TYPES
      INTEGER IER
!      INTEGER (KIND=MPI_ADDRESS_KIND) :: EXTENT,ILB,IUB,INTEX
      INTEGER (KIND=MY_ADDRESS_KIND) :: EXTENT,ILB,IUB,INTEX
!
      INTEGER I
!
      CH_BLENGTH=(/1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1/)
      CH_DELTA=  (/0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0/)
!
!     TERMINATE ON MPI PARTS NOT HANDLED BY COMPAD
#  if defined COMPAD
      WRITE(LU,*) 'ORG_CHARAC_TYPE_OIL: COMPAD MAYBE CRITICAL',
     &     'MPI OPERATION'
      WRITE(LU,*) '  PLEASE CONTACT JR @ ADJOINTWARE'
      CALL PLANTE(1)
      STOP
#  endif
!     INTEGERS IN THE STRUCTURE
!
      CALL P_MPI_TYPE_GET_EXTENT(MPI_INTEGER,ILB,INTEX,IER)
      CH_DELTA(1)=0
!     9 IS THE FIRST DOUBLE PRECISION THAT COMES AFTER AN INTEGER
      DO I=2,9
        CH_DELTA(I)=CH_DELTA(I-1)+INTEX
      ENDDO
!
!     DOUBLE PRECISION IN THE STRUCTURE
!
!     Handle components of active data type correctly
#ifdef COMPAD
      CALL P_MPI_TYPE_GET_EXTENT(AMPI_TYPE,ILB,INTEX,IER)
#else
      CALL P_MPI_TYPE_GET_EXTENT(MPI_DOUBLE_PRECISION,ILB,INTEX,IER)
#endif
!     THE 11 REMAINING DOUBLE PRECISION
      CH_DELTA(10)=CH_DELTA(9)+INTEX
      CH_DELTA(11)=CH_DELTA(10)+INTEX
      CH_DELTA(12)=CH_DELTA(11)+INTEX
      CH_DELTA(13)=CH_DELTA(12)+INTEX
      CH_DELTA(14)=CH_DELTA(13)+INTEX
!     ADDRESS AFTER THE BASKETCOMPO TABLE
      CH_DELTA(15)=CH_DELTA(14)+INTEX*MAX_BASKET_SIZE
      CH_DELTA(16)=CH_DELTA(15)+INTEX*MAX_BASKET_SIZE
!     ADDRESS AFTER THE BASKETHAP TABLE
      CH_DELTA(17)=CH_DELTA(16)+INTEX*MAX_BASKET_SIZE
      CH_DELTA(18)=CH_DELTA(17)+INTEX*MAX_BASKET_SIZE
      CH_DELTA(19)=CH_DELTA(18)+INTEX*MAX_BASKET_SIZE
!     ADDRESS AFTER THE LAST HAP TABLE
      CH_DELTA(20)=CH_DELTA(19)+INTEX*MAX_BASKET_SIZE
!
      CH_BLENGTH(14) = MAX_BASKET_SIZE
      CH_BLENGTH(15) = MAX_BASKET_SIZE
      CH_BLENGTH(16) = MAX_BASKET_SIZE
      CH_BLENGTH(17) = MAX_BASKET_SIZE
      CH_BLENGTH(18) = MAX_BASKET_SIZE
      CH_BLENGTH(19) = MAX_BASKET_SIZE
!
      CH_TYPES(1)=MPI_INTEGER
      CH_TYPES(2)=MPI_INTEGER
      CH_TYPES(3)=MPI_INTEGER
      CH_TYPES(4)=MPI_INTEGER
      CH_TYPES(5)=MPI_INTEGER
      CH_TYPES(6)=MPI_INTEGER
      CH_TYPES(7)=MPI_INTEGER
      CH_TYPES(8)=MPI_INTEGER
!! COMPAD-DCO  BEGIN  JR2017
!!   Handle components of active data type correctly
#if defined COMPAD
      CH_TYPES(9)=AMPI_TYPE
      CH_TYPES(10)=AMPI_TYPE
      CH_TYPES(11)=AMPI_TYPE
      CH_TYPES(12)=AMPI_TYPE
      CH_TYPES(13)=AMPI_TYPE
      CH_TYPES(14)=AMPI_TYPE
      CH_TYPES(15)=AMPI_TYPE
      CH_TYPES(16)=AMPI_TYPE
      CH_TYPES(17)=AMPI_TYPE
      CH_TYPES(18)=AMPI_TYPE
      CH_TYPES(19)=AMPI_TYPE
#else
      CH_TYPES(9)=MPI_DOUBLE_PRECISION
      CH_TYPES(10)=MPI_DOUBLE_PRECISION
      CH_TYPES(11)=MPI_DOUBLE_PRECISION
      CH_TYPES(12)=MPI_DOUBLE_PRECISION
      CH_TYPES(13)=MPI_DOUBLE_PRECISION
      CH_TYPES(14)=MPI_DOUBLE_PRECISION
      CH_TYPES(15)=MPI_DOUBLE_PRECISION
      CH_TYPES(16)=MPI_DOUBLE_PRECISION
      CH_TYPES(17)=MPI_DOUBLE_PRECISION
      CH_TYPES(18)=MPI_DOUBLE_PRECISION
      CH_TYPES(19)=MPI_DOUBLE_PRECISION
#endif
      CH_TYPES(20)=MPI_UB       ! THE TYPE UPPER BOUND MARKER
      CALL P_MPI_TYPE_CREATE_STRUCT(20,CH_BLENGTH,CH_DELTA,CH_TYPES,
     &                              OIL_CHARAC,IER)
      CALL P_MPI_TYPE_COMMIT(OIL_CHARAC,IER)
      CALL P_MPI_TYPE_GET_EXTENT(OIL_CHARAC,ILB,EXTENT,IER)
      IUB=ILB+EXTENT
!
      IF(ILB.NE.CH_DELTA(1).OR.IUB.NE.CH_DELTA(20)) THEN
        WRITE(LU,*) ' PARALLEL::ORG_CHARAC_TYPE_OIL:'
        WRITE(LU,*) ' MEMORY PROBLEM WITH THIS COMPILER: '
        WRITE(LU,*) ' ILB=',ILB,' NOT EQUAL TO CH_DELTA(1)=',
     &        CH_DELTA(1)
        WRITE(LU,*) ' OR'
        WRITE(LU,*) ' IUB=',IUB,' NOT EQUAL TO CH_DELTA(20)=',
     &        CH_DELTA(20)
        CALL PLANTE(1)
        STOP
      ENDIF
!
!----------------------------------------------------------------------
!
      RETURN
      END SUBROUTINE ORG_CHARAC_TYPE_OIL
